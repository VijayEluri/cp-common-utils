apply plugin: 'java'
apply plugin: 'idea'

task wrapper(type: Wrapper) {
	gradleVersion = '2.0'
	distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
	group = "com.complexible.common"
	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'

	repositories {
		mavenCentral()
	}
}

ext {
	projectDescription	= "A collection of basic utility classes for various common tasks. " +
						 "Classes provided here often extend or supplement the functionality provided by Guava."
	projectUrl			= "https://github.com/mhgrove/cp-common-utils"
}

subprojects {
	apply plugin: 'java'

	dependencies {
		compile 'org.slf4j:slf4j-api:1.7.7'
		compile 'org.slf4j:slf4j-jdk14:1.7.7'

		testCompile 'junit:junit:4.11'
	}

	sourceSets {
		main {
			java {
				srcDir 'main/src'
			}
			resources {
				srcDir 'main/resources'
			}
		}
		test {
			java {
				srcDir 'test/src'
			}
			resources {
				srcDir 'test/resources'
			}
		}
	}

	// create a 'tests' conf for importing test classes from other sub-projects
	task testJar(type: Jar, dependsOn: testClasses) {
		classifier = 'tests'
		from sourceSets.test.output
	}

	artifacts {
		testRuntime testJar // include the tests of other subprojects
	}

	apply from: "$rootDir/maven.gradle"
}

evaluationDependsOnChildren()

subprojects {
	task javadocs(type: Javadoc) {
		println "generating docs: ${archivesBaseName}-${project.version}"
		source sourceSets.main.allJava
		classpath = configurations.compile
		destinationDir = file("${rootProject.buildDir}/javadocs/${project.name}")
		options.addBooleanOption("nodeprecatedlist", false)
		options.addBooleanOption("nodeprecated", false)
		options.addStringOption('Xdoclint:none', '-quiet')
		configure(options) {
			windowTitle "${project.archivesBaseName}-${project.version} API"
			docTitle "${project.archivesBaseName}-${project.version}"
			bottom "Copyright &#169; 2010-2016 Complexible. All Rights Reserved."
			links "http://docs.oracle.com/javase/8/docs/api/",
				  "http://docs.guava-libraries.googlecode.com/git-history/v18.0/javadoc/"
		}
	}
}


